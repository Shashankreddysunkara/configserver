plugins {
	id 'org.springframework.boot' version '2.4.9'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'maven-publish'
	id "org.sonarqube" version "3.3"
	id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2020.0.3")
}

dependencies {
	implementation 'org.springframework.cloud:spring-cloud-config-server'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.withType(GenerateModuleMetadata) {
	enabled = false
}

configurations {
	[apiElements, runtimeElements].each {
		it.outgoing.artifacts.removeIf {
			it.buildDependencies.getDependencies(null).contains(jar) }
		it.outgoing.artifact(bootJar)
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact source: jar
		}
	}
}

sonarqube {
	properties {
		property "sonar.projectKey", "ggnanasekaran77_configserver"
		property "sonar.organization", "ggnanasekaran77"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

jacocoTestReport {
	reports {
		xml.enabled true
		csv.enabled false
		html.enabled false
	}
}

test.finalizedBy jacocoTestReport

test {
	useJUnitPlatform()
}

